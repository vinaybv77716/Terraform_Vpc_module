pipeline {

    agent any

    tools {
        terraform 'terraform'
    }

    options {
        disableConcurrentBuilds()
        skipDefaultCheckout(true)
        timeout(time: 15, unit: 'MINUTES')
    }

    parameters {
        booleanParam(
            name: 'autoApprove',
            defaultValue: false,
            description: 'Automatically run apply after generating plan?'
        )
    }

    environment {
        AWS_ACCESS_KEY_ID     = credentials('AWS_ACCESS_KEY_ID')
        AWS_SECRET_ACCESS_KEY = credentials('AWS_SECRET_ACCESS_KEY')
    }

    stages {

        stage('Checkout') {
            steps {
                dir('terraform') {
                    git branch: 'main',
                        credentialsId: 'gitlab_cred',
                        url: 'https://gitlab.com/nikita_kakade-group/new-project.git'
                }
            }
        }

        stage('Plan') {
            steps {
                dir('terraform') {
                    sh 'terraform init -reconfigure'
                    sh 'terraform plan -out=tfplan'
                    sh 'terraform show -no-color tfplan > tfplan.txt'
                }
            }
        }

        stage('Approval') {
            when {
                not { equals expected: true, actual: params.autoApprove }
            }
            steps {
                script {
                    echo "Waiting for manual approval..."
                    timeout(time: 5, unit: 'MINUTES') {
                        input(
                            message: 'Approve Terraform apply?',
                            ok: 'Apply',
                            submitter: 'nikita'
                        )
                    }
                }
            }
        }

        stage('Apply') {
            when {
                anyOf {
                    equals expected: true, actual: params.autoApprove
                    expression { currentBuild.resultIsBetterOrEqualTo('SUCCESS') }
                }
            }
            steps {
                dir('terraform') {
                    sh 'terraform apply -input=false tfplan'
                }
            }
        }
    }
}
